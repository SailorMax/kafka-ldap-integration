buildscript {
    ext.kotlin_version = '1.2.70'
    ext.kafka_version = '2.0.0'
    ext.unboundid_version = '4.0.8'
    ext.caffeine_version = '2.6.2'
    ext.jackson_version = '2.9.6'
    ext.spek_version = '1.1.5'
    ext.kluent_version = '1.39'
    ext.slf4j_version = '1.7.25'

}

plugins{
    id "org.jetbrains.kotlin.jvm" version "1.2.70"
    id 'org.jmailen.kotlinter' version '1.15.0'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group 'no.nav.common.security'

// see https://docs.confluent.io/current/installation/versions-interoperability.html#interoperability-versions
// '<kafka version>_<this plugin version>'
version '2.0_0.52'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.unboundid:unboundid-ldapsdk:$unboundid_version"

    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version"

    implementation "org.apache.kafka:kafka_2.12:$kafka_version"
    implementation "com.github.ben-manes.caffeine:caffeine:$caffeine_version"

    testCompile ("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }

    testImplementation 'org.junit.platform:junit-platform-runner:1.2.0'
    testImplementation "org.amshove.kluent:kluent:$kluent_version"

    testRuntimeOnly ("org.jetbrains.spek:spek-junit-platform-engine:$spek_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }

    testRuntime "org.slf4j:slf4j-jdk14:$slf4j_version"
}

configurations.all {
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform {
        includeEngines 'spek'
    }
}

shadowJar {

    classifier = null

    dependencies {
        exclude(dependency('org.apache.kafka::'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-databind:'))
        exclude(dependency('org.lz4::'))
        exclude(dependency('org.xerial.snappy::'))
        exclude(dependency('org.slf4j::'))
        exclude(dependency('com.yammer.metrics::'))
        exclude(dependency('com.101tec::'))
        exclude(dependency('org.apache.zookeeper::'))
        exclude(dependency('log4j::'))
        exclude(dependency('net.sf.jopt-simple::'))
        exclude(dependency('org.scala-lang::'))
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    repositories {
        maven {
            credentials {
                username findProperty("nexusUser")
                password findProperty("nexusPassword")
            }
            url findProperty("nexusURL")
        }
    }
}

task copyJarToKafka(type: Copy) {
    from findProperty("kafkaJARSrc") ?: ''
    into findProperty("kafkaJARDest") ?: ''
}